#!/bin/zsh

export DOTFILES="$HOME/.dotfiles"
ZSH_CONFIG=$DOTFILES/features/zsh/index.zsh
source "$DOTFILES"/features/utils.sh

TIME_START=$(os_time)

local compute_load_time () {
  local START=$1
  if [[ $DOTFILES_DEBUG = "1" ]]; then
    END=$(os_time)
    echo "$((END-START))ms"
  fi
}

local source_local () {
  if [[ -a $DOTFILES/.local ]]; then
    source $DOTFILES/.local
  fi
}

local source_zsh_index () {
  source "$ZSH_CONFIG"
}

source_path_files () {
  typeset -U FILES

  OS_SUFFIX=$(os_suffix)
  setopt local_options null_glob
  FILES=($DOTFILES/features/*/path.zsh.$OS_SUFFIX $DOTFILES/features/*/path.zsh $DOTFILES/extensions/**/path.zsh.$OS_SUFFIX $DOTFILES/extensions/**/path.zsh)

  for FILE in $FILES; do
    source $FILE
  done

  unset FILES
}

source_features_indexes () {
  typeset -U FILES

  OS_SUFFIX=$(os_suffix)
  setopt local_options null_glob
  FILES=($DOTFILES/features/*/index.zsh.$OS_SUFFIX $DOTFILES/features/*/index.zsh $DOTFILES/extensions/**/index.zsh.$OS_SUFFIX $DOTFILES/extensions/**/index.zsh)

  for FILE in $FILES; do
    if [ $FILE != $ZSH_CONFIG ]; then
      source $FILE
    fi
  done

  unset FILES
}

source_local               # 1. Load local-only variables (not commited).
source_zsh_index           # 2. Load zsh config index.zsh file, which sets critical variables.
source_path_files          # 3. Load files that define PATH. These are loaded before the features so they can rely on PATH being set correctly, without depending on loading order.
source_features_indexes    # 4. Load features index.zsh files.

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

compute_load_time $TIME_START
